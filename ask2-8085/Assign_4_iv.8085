	IN  10H	;Αρση προσασιας μνημης

START: LXI H,0A05H	;Θεση μνημης που θα βαλουμε το αποτελεσμα
	LXI B,0000H	;Αρχικοποιηση καταχωρητων
	LXI D,0000H
	MVI A,00H

	MVI M,10H	;Θετουμε σβηστα τα 3 πρωτα BCD
	DCX H
	MVI M,10H
	DCX H
	MVI M,10H

	LDA 2000H	;Διαβαζουμε την εισοδο
	MOV B,A	;Την κραταμε στο Β
	RAL		;Βλεπουμε αν ειναι 1 το MSB
	CC TOPOS	;Αν ειναι πρεπει να κανουμε συμπληρωμα 2

	MOV A,B	;Παιρνουμε τον θετικο πλεον αριθμο που εχουμε να απεικονισουμε
	CPI 64H	;Τον συγκρινουμε με το 100d
	CNC HUN	;Αν ειναι μεγαλυτερος ή ισος του 100 παμε στη ρουτινα HUN
CHECK:	CPI 0AH	;Συγκρινουμε με το 10	
	JNC GRTEN	;Αν ειναι μεγαλυτερο ή ισο παμε στο GRTEN
	MOV E,A	;Κραταμε και τις μοναδες
	
	DCX H
	MOV M,C	;Βαζουμε τις εκατονταδες στο 4ο BCD
	DCX H
	MOV M,D	;Βαζουμε τις δεκαδες στο 5ο BCD
	DCX H
	MOV M,E	;Βαζουμε τις μοναδες στο 6ο BCD

	LXI D,0A00H	;Βάζουμε στον D-E την θέση μνήμης όπου αρχίζουν τα	 
	CALL STDM	;δεδομένα που θέλουμε να δείξουμε καλούμε την STDM
	CALL DCD	;και εμφανιζουμε με την DCD
	JMP START

TOPOS:	MOV A,B	;Παιρνουμε τον αριθμο
	CMA		;Τον αντιστρεφουμε
	ADI 01H	;Προσθετουμε 1
	MOV B,A	;Τον κραταμε στο Β
	MVI M,1CH	;Βαζουμε στο 3ο BCD το μειον
	RET

HUN:	MVI C,01H	;Βαζουμε στο C 1 οπου ειναι οι εκατονταδες
	SBI 64H	;Αφαιρουμε 100 και επιστρεφουμε
	RET

GRTEN:	INR D		;Αυξανουμε τις δεκαδες
	SBI 0AH	;Αφαιρουμε 10 απο τον αριθμο και επιστρεφουμε
	JMP CHECK

END